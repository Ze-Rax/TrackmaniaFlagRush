/**
 * Holds current state of the flag.
 * Provides functions to update the state on demand.
 * Call lifecycle functions in main mode.
 *
 */

#Include "MathLib" as ML
#Include "Libs/Zrx/ModeLibs/FlagRush/UI/Modules/Markers.Script.txt" as FlagRush_Markers

#Struct K_Player {
	Ident PlayerId;
	Text Login;
	Integer Clan;
}

#Struct K_FlagPass {
	K_Player OldCarrier;
	K_Player NewCarrier;
	Integer Date;
}

#Struct K_FlagDrop {
	K_Player OldCarrier;
	Integer Date;
}

#Struct K_FlagState {
	CMapLandmark Landmark;
	K_Player Carrier;
	Vec3 Position;
	Vec3 PrevPosition;
	Vec3 LastSafePosition;
	Integer CurrentStateStartDate;
	Integer CurrentStateEndDate;
	Boolean HasStealResistance;
	K_FlagDrop LastDrop;
	K_FlagPass LastFlagPass;
}

#Struct K_Net_FlagHolder {
	Integer Clan;
	Text Name;
	Text Login;
}

#Struct K_Net_FlagState {
	K_Net_FlagHolder Holder;
	Vec3 Position;
	Integer CurrentStateStartDate;
	Integer CurrentStateEndDate;
	Boolean HasStealResistance;
}

#Const C_FlagCarrierName_Dropped 		"Dropped"
#Const C_FlagCarrierName_Spawn			"Spawn"
#Const C_FlagCarrierName_Unspawned	"Unspawned"
#Const C_DropVerticalOffset 				0.5
#Const C_Position_Invalid						<-1., -1., -1.>

declare K_FlagState G_FlagState;

/**
 * Transforms a CSmPlayer into a K_Player struct.
 */
K_Player Private_PlayerToStruct(CSmPlayer Player) {
	return K_Player { PlayerId = Player.Id, Login = Player.User.Login, Clan = Player.CurrentClan };
}

/**
 * Gets the current flag state.
 */
K_FlagState Get() {
	return G_FlagState;
}

/**
 * Gets the current flag carrier CSmPlayer.
 * Null if player not found.
 */
 CSmPlayer GetFlagCarrierPlayer() {
	if (Players.existskey(G_FlagState.Carrier.PlayerId)) {
		return Players[G_FlagState.Carrier.PlayerId];
	}
	return Null;
}

/**
 * Returns true if the flag is carried by a player.
 */
Boolean IsOnPlayer() {
	return G_FlagState.Carrier.PlayerId != NullId;
}

/**
 * Returns true if the flag is set on a landmark.
 */
Boolean IsOnLandmark() {
	return G_FlagState.Landmark != Null;
}

/**
 * Returns true if the flag is at position C_Position_Invalid.
 */
Boolean IsUnspawned() {
	return G_FlagState.Position == C_Position_Invalid;
}

/**
 * Return true if the flag is not carried, not at a landmark, but at a valid position (!= C_Position_Invalid).
 */
Boolean IsDropped() {
	return !IsOnPlayer() && !IsOnLandmark() && !IsUnspawned();
}

/**
 * Flag is carried, but the player objects doesn't exist in the Players array.
 */
Boolean CarrierDisconnected() {
	return IsOnPlayer() && !Players.existskey(G_FlagState.Carrier.PlayerId);
}

/**
 * Flag is carried and distance between now and last yield is greater than the provided Threshold.
 */
Boolean CarrierTeleported(Real Threshold) {
	return IsOnPlayer() && ML::Distance(G_FlagState.PrevPosition, G_FlagState.Position) > Threshold;
}

/**
 * Send data about the flag carrier, i.e. name and clan to the UIs via netwrite.
 */
 Void Private_Net_SendFlagState() {
	declare netwrite K_Net_FlagState FlagRush_Net_FlagState for Teams[0];
	FlagRush_Net_FlagState.Holder = K_Net_FlagHolder{};

	declare CSmPlayer FlagCarrierPlayer = GetFlagCarrierPlayer();
	if (FlagCarrierPlayer != Null) {
		FlagRush_Net_FlagState.Holder.Clan = FlagCarrierPlayer.CurrentClan;
		FlagRush_Net_FlagState.Holder.Name = FlagCarrierPlayer.User.Name;
		FlagRush_Net_FlagState.Holder.Login = FlagCarrierPlayer.User.Login;
	} else {
		FlagRush_Net_FlagState.Holder.Clan = 0; // Not carried => Neutral

		if (IsOnLandmark()) {
			FlagRush_Net_FlagState.Holder.Name = C_FlagCarrierName_Spawn;
		} else if (IsDropped()) {
			FlagRush_Net_FlagState.Holder.Name = C_FlagCarrierName_Dropped;
		} else {
			FlagRush_Net_FlagState.Holder.Name = C_FlagCarrierName_Unspawned;
		}
	}

	FlagRush_Net_FlagState.Position = G_FlagState.Position;
	FlagRush_Net_FlagState.CurrentStateStartDate = G_FlagState.CurrentStateStartDate;
	FlagRush_Net_FlagState.CurrentStateEndDate = G_FlagState.CurrentStateEndDate;
	FlagRush_Net_FlagState.HasStealResistance = G_FlagState.HasStealResistance;
}

/**
 * Sets the flag state to be at a landmark.
 * Also updates the position, the marker and unsets the carrier
 */
Void SetAtLandmark(CMapLandmark Landmark, Integer CurrentStateEndDate) {
	G_FlagState.Landmark = Landmark;
	G_FlagState.Position = Landmark.Position;
	G_FlagState.Carrier = K_Player{};
	G_FlagState.CurrentStateStartDate = Now;
	G_FlagState.CurrentStateEndDate = CurrentStateEndDate;
	G_FlagState.HasStealResistance = False;
	Private_Net_SendFlagState();
	FlagRush_Markers::FlagMarker_SetOnLandmark(Landmark);
}


Void SetAtLandmark(CMapLandmark Landmark) {
	SetAtLandmark(Landmark, -1);
}

/**
 * Sets the flag state to be at a player.
 * Also updates the position, the marker and unsets the landmark.
 */
Void SetAtCarrier(CSmPlayer Carrier, Integer CurrentStateEndDate, Boolean HasStealResistance) {
	G_FlagState.Carrier = Private_PlayerToStruct(Carrier);
	G_FlagState.Landmark = Null;
	G_FlagState.Position = Carrier.Position;
	G_FlagState.CurrentStateStartDate = Now;
	G_FlagState.CurrentStateEndDate = CurrentStateEndDate;
	G_FlagState.HasStealResistance = HasStealResistance;
	Private_Net_SendFlagState();
	FlagRush_Markers::FlagMarker_SetOnPlayer(Carrier);
}

Void SetAtCarrier(CSmPlayer Carrier, Boolean HasStealResistance) {
	SetAtCarrier(Carrier, -1, HasStealResistance);
}

/**
 * Sets the flag state to be at a position.
 * Also updates the marker and unsets the landmark and the carrier.
 */
Void SetAtPosition(Vec3 Position, Integer CurrentStateEndDate) {
	 G_FlagState.Position = Position;
	 G_FlagState.Carrier = K_Player{};
	 G_FlagState.Landmark = Null;
	 G_FlagState.CurrentStateStartDate = Now;
	 G_FlagState.CurrentStateEndDate = CurrentStateEndDate;
	 G_FlagState.HasStealResistance = False;
	 Private_Net_SendFlagState();
	 if (Position == C_Position_Invalid) {
		FlagRush_Markers::FlagMarker_Remove();
	 } else {
	 	FlagRush_Markers::FlagMarker_SetOnPosition(Position);
	 }
	}


Void SetAtPosition(Vec3 Position) {
	SetAtPosition(Position, -1);
}

/**
 * Sets the position to C_Position_Invalid. At this state it's considered neither carried, on flagspawn or dropped
 */
Void SetUnspawned() {
	SetAtPosition(C_Position_Invalid);
}

/**
 * Sets the position of the flag and updates the last drop date.
 */
Void Drop(Vec3 Position, Integer CurrentStateEndDate) {
	declare Boolean FlagRush_Debug for This;
	if (FlagRush_Debug) {
		assert(IsOnPlayer(), "Cannot drop a flag that is not being carried by anyone");
	} else if (!IsOnPlayer()) {
		return;
	}

	G_FlagState.LastDrop = K_FlagDrop{ Date = Now, OldCarrier = G_FlagState.Carrier };
	SetAtPosition(Position + <0., C_DropVerticalOffset, 0.>, CurrentStateEndDate);
}

/**
 * Sets the new flag carrier and also tracks the last pass.
 */
Void Pass(CSmPlayer NewCarrier, Integer CurrentStateEndDate, Boolean HasStealResistance) {
	declare Boolean FlagRush_Debug for This;
	if (FlagRush_Debug) {
		assert(IsOnPlayer(), "Cannot pass a flag that is not being carried by anyone");
		assert(NewCarrier != GetFlagCarrierPlayer(), "Cannot pass the flag to the same player");
	} else if (!IsOnPlayer() || NewCarrier == GetFlagCarrierPlayer()) {
		return;
	}

	G_FlagState.LastFlagPass = K_FlagPass{
		OldCarrier = G_FlagState.Carrier,
		NewCarrier = Private_PlayerToStruct(NewCarrier),
		Date = Now
	};
	SetAtCarrier(NewCarrier, CurrentStateEndDate, HasStealResistance);
}

/* Lifecycle */

Void Yield() {
	declare CSmPlayer CarrierPlayer = GetFlagCarrierPlayer();
	if (CarrierPlayer != Null) {
		G_FlagState.PrevPosition = G_FlagState.Position;
		G_FlagState.Position = CarrierPlayer.Position;
		if (CarrierPlayer.WheelsContactCount >= 3) {
			G_FlagState.LastSafePosition = CarrierPlayer.Position;
		}
	}
}

Void Unload() {
	G_FlagState = K_FlagState{};
	SetUnspawned();
}

Void Load() {
	Unload();
}