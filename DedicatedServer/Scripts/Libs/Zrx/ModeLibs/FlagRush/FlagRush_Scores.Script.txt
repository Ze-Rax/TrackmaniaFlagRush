#Include "Libs/Nadeo/TMxSM/Race/Scores.Script.txt" as Scores
#Include "Libs/Zrx/ModeLibs/Common/PlayerTelemetry.Script.txt" as Telemetry
#Include "Libs/Zrx/ModeLibs/Common/TeamPointsProgression.Script.txt" as TeamPointsProgression

#Const	C_PointsValue_ScoreFlag				3
#Const	C_PointsValue_StealFlag				1
#Const	C_PointsValue_ScoreFlagAssist	1

#Const	C_Telemetry_Key_FlagsScored				"FlagsScored"
#Const	C_Telemetry_Key_FlagsStolen				"FlagsStolen"
#Const	C_Telemetry_Key_FlagScoreAssists	"FlagScoreAssists"

/* Teams */

Integer GetClanRoundPoints(Integer Clan) {
	return Scores::GetClanRoundPoints(Clan);
}

Integer GetClanMapPoints(Integer Clan) {
	return TeamPointsProgression::GetClanRoundsWon(Clan);
}

Integer GetClanMatchPoints(Integer Clan) {
	return TeamPointsProgression::GetClanMapsWon(Clan);
}

Void SetClanRoundPoints(Integer Clan, Integer Value) {
	Scores::SetClanRoundPoints(Clan, Value);
}

Void SetClanMapPoints(Integer Clan, Integer Value) {
	TeamPointsProgression::SetClanRoundsWon(Clan, Value);
}

Void SetClanMatchPoints(Integer Clan, Integer Value) {
	TeamPointsProgression::SetClanMapsWon(Clan, Value);
}

/* Players */

Integer GetPlayerRoundPoints(CSmScore Score) {
	return Scores::GetPlayerRoundPoints(Score);
}

Integer GetPlayerMapPoints(CSmScore Score) {
	return Scores::GetPlayerMapPoints(Score);
}

Integer GetPlayerMatchPoints(CSmScore Score) {
	return Scores::GetPlayerMatchPoints(Score);
}

Integer GetPlayerRoundFlagScores(CSmScore Score) {
	return Telemetry::GetPlayerRoundInteger(Score, C_Telemetry_Key_FlagsScored);
}

Integer GetPlayerMapFlagScores(CSmScore Score) {
	return Telemetry::GetPlayerMapInteger(Score, C_Telemetry_Key_FlagsScored);
}

Integer GetPlayerMatchFlagScores(CSmScore Score) {
	return Telemetry::GetPlayerMatchInteger(Score, C_Telemetry_Key_FlagsScored);
}

Integer GetPlayerRoundFlagScoreAssists(CSmScore Score) {
	return Telemetry::GetPlayerRoundInteger(Score, C_Telemetry_Key_FlagScoreAssists);
}

Integer GetPlayerMapFlagScoreAssists(CSmScore Score) {
	return Telemetry::GetPlayerMapInteger(Score, C_Telemetry_Key_FlagScoreAssists);
}

Integer GetPlayerMatchFlagScoreAssists(CSmScore Score) {
	return Telemetry::GetPlayerMatchInteger(Score, C_Telemetry_Key_FlagScoreAssists);
}

Integer GetPlayerRoundFlagSteals(CSmScore Score) {
	return Telemetry::GetPlayerRoundInteger(Score, C_Telemetry_Key_FlagsStolen);
}

Integer GetPlayerMapFlagSteals(CSmScore Score) {
	return Telemetry::GetPlayerMapInteger(Score, C_Telemetry_Key_FlagsStolen);
}

Integer GetPlayerMatchFlagSteals(CSmScore Score) {
	return Telemetry::GetPlayerMatchInteger(Score, C_Telemetry_Key_FlagsStolen);
}

/* Net send scores */

Void Net_SendTeamScores() {
	for(Clan, 1, 2) {
		declare netwrite Integer Net_FlagRush_TeamRoundScore for Teams[Clan-1];
		Net_FlagRush_TeamRoundScore = Scores::GetClanRoundPoints(Clan);
		declare netwrite Integer Net_FlagRush_TeamMapScore for Teams[Clan-1];
		Net_FlagRush_TeamMapScore = TeamPointsProgression::GetClanRoundsWon(Clan);
		declare netwrite Integer Net_FlagRush_TeamMatchScore for Teams[Clan-1];
		Net_FlagRush_TeamMatchScore = TeamPointsProgression::GetClanMapsWon(Clan);
	}
}

Void Net_SendPlayerScore(CSmScore Score) {
	declare netwrite Integer Net_FlagRush_Points_Match for Score;
	declare netwrite Integer Net_FlagRush_FlagsScored_Match for Score;
	declare netwrite Integer Net_FlagRush_FlagsScored_Map for Score;
	declare netwrite Integer Net_FlagRush_FlagsScored_Round for Score;
	declare netwrite Integer Net_FlagRush_FlagsStolen_Match for Score;
	declare netwrite Integer Net_FlagRush_FlagsStolen_Map for Score;
	declare netwrite Integer Net_FlagRush_FlagsStolen_Round for Score;
	declare netwrite Integer Net_FlagRush_FlagsAssists_Match for Score;
	declare netwrite Integer Net_FlagRush_FlagsAssists_Map for Score;
	declare netwrite Integer Net_FlagRush_FlagsAssists_Round for Score;

	Net_FlagRush_Points_Match = GetPlayerMatchPoints(Score);

	Net_FlagRush_FlagsScored_Match = GetPlayerMatchFlagScores(Score);
	Net_FlagRush_FlagsScored_Map = GetPlayerMapFlagScores(Score);
	Net_FlagRush_FlagsScored_Round = GetPlayerRoundFlagScores(Score);

	Net_FlagRush_FlagsStolen_Match = GetPlayerRoundFlagSteals(Score);
	Net_FlagRush_FlagsStolen_Map = GetPlayerMapFlagSteals(Score);
	Net_FlagRush_FlagsStolen_Round = GetPlayerMatchFlagSteals(Score);

	Net_FlagRush_FlagsAssists_Match = GetPlayerRoundFlagScoreAssists(Score);
	Net_FlagRush_FlagsAssists_Map = GetPlayerMapFlagScoreAssists(Score);
	Net_FlagRush_FlagsAssists_Round = GetPlayerMatchFlagScoreAssists(Score);
}

Void Net_SendScores() {
	Net_SendTeamScores();
	foreach (Score in Scores) Net_SendPlayerScore(Score);
}

/* Events */

Void OnClanRoundWin(Integer Clan) {
	TeamPointsProgression::AddClanRoundsWon(Clan, 1);
	Net_SendTeamScores();
}

Void OnClanMapWin(Integer Clan) {
	TeamPointsProgression::AddClanMapsWon(Clan, 1);
	Net_SendTeamScores();
}

Void OnPlayerScoreFlag(CSmPlayer Player) {
	Telemetry::AddPlayerRoundInteger(Player.Score, C_Telemetry_Key_FlagsScored, 1);
	Scores::AddPlayerRoundPoints(Player.Score, C_PointsValue_ScoreFlag);
	Scores::AddClanRoundPoints(Player.Score.TeamNum, 1); // TeamNum == Clan == Teams[]_Index + 1
	Net_SendPlayerScore(Player.Score);
}

Void OnPlayerScoreFlagAssist(CSmPlayer Player) {
	Telemetry::AddPlayerRoundInteger(Player.Score, C_Telemetry_Key_FlagScoreAssists, 1);
	Scores::AddPlayerRoundPoints(Player.Score, C_PointsValue_ScoreFlagAssist);
	Net_SendPlayerScore(Player.Score);
}

Void OnPlayerScoreFlag(CSmPlayer Scorer, CSmPlayer Assist) {
	OnPlayerScoreFlag(Scorer);
	OnPlayerScoreFlagAssist(Assist);
	Net_SendPlayerScore(Scorer.Score);
	Net_SendPlayerScore(Assist.Score);
}

Void OnPlayerStealFlag(CSmPlayer Player) {
	Telemetry::AddPlayerRoundInteger(Player.Score, C_Telemetry_Key_FlagsStolen, 1);
	Scores::AddPlayerRoundPoints(Player.Score, C_PointsValue_StealFlag);
	Net_SendPlayerScore(Player.Score);
}

/* Progression */

Void StartMatch() {
	Scores::StartMatch();
	Telemetry::StartMatch();
	TeamPointsProgression::StartMatch();
}

Void EndMatch() {
	Scores::EndMatch();
	Telemetry::EndMatch();
	TeamPointsProgression::EndMatch();
	Net_SendScores();
}

Void StartMap() {
	Scores::StartMap();
	Telemetry::StartMap();
	TeamPointsProgression::StartMap();
}

Void EndMap() {
	Scores::EndMap();
	Telemetry::EndMap();
	TeamPointsProgression::EndMap();
	Net_SendScores();
}

Void StartRound() {
	Scores::StartRound();
	Telemetry::StartRound();
	TeamPointsProgression::StartRound();
}

Void EndRound() {
	Scores::EndRound();
	Telemetry::EndRound();
	TeamPointsProgression::EndRound();
	Net_SendScores();
}