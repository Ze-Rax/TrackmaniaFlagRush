#Include "/Libs/Zrx/ModeLibs/FlagRush/UI/FlagRush_ColorPalette.Script.txt"	as Colors
#Include "/Libs/Rx/ModeLibs/Common/NiceColors.Script.txt"	as NCL
#Include "MathLib" as ML
#Include "TextLib" as TL

#Const C_Hue_UseDefault			-1
#Const C_DefaultTeamHue			120
#Const C_DefaultTeamHues		[0 => 240, 1 => 0]

#Struct K_TeamColor {
	Text TeamName;
	Text SkinName;
}

#Const C_TeamColorLut [
	K_TeamColor{ TeamName = "Red", SkinName = "https://core.trackmania.nadeo.live/storageObjects/f94a342c-cbd2-4136-953c-b5da764b487e" },
	K_TeamColor{ TeamName = "Orange", SkinName = "https://core.trackmania.nadeo.live/storageObjects/b9f750a2-38d5-4ef8-b9c4-7e2820014b2c" },
	K_TeamColor{ TeamName = "Yellow", SkinName = "https://core.trackmania.nadeo.live/storageObjects/edf2a1fd-3326-460e-ae27-babe0d501ee9" },
	K_TeamColor{ TeamName = "Lime", SkinName = "https://core.trackmania.nadeo.live/storageObjects/6a7361fe-483e-4cab-92de-5276438764dc" },
	K_TeamColor{ TeamName = "Green", SkinName = "https://core.trackmania.nadeo.live/storageObjects/779dfacc-5b75-4619-abbd-1d698e25813d" },
	K_TeamColor{ TeamName = "Teal", SkinName = "https://core.trackmania.nadeo.live/storageObjects/808f84d2-d41d-4fb6-8df7-2b7f404463ef" },
	K_TeamColor{ TeamName = "Cyan", SkinName = "https://core.trackmania.nadeo.live/storageObjects/370a2a6a-1847-4e2a-ac17-4bcef7568cbc" },
	K_TeamColor{ TeamName = "Azure", SkinName = "https://core.trackmania.nadeo.live/storageObjects/570227b5-418b-4cee-b704-1221d3105f58" },
	K_TeamColor{ TeamName = "Blue", SkinName = "https://core.trackmania.nadeo.live/storageObjects/1842cac7-75e2-4457-a2f4-fad48d67ef03" },
	K_TeamColor{ TeamName = "Purple", SkinName = "https://core.trackmania.nadeo.live/storageObjects/d2260262-6fbb-4488-af6a-a01ed5279188" },
	K_TeamColor{ TeamName = "Magenta", SkinName = "https://core.trackmania.nadeo.live/storageObjects/e8e53aa9-7072-4f02-9dba-3a9bf0d7ddd6" },
	K_TeamColor{ TeamName = "Rose", SkinName = "https://core.trackmania.nadeo.live/storageObjects/750eb055-6913-4cdd-a8b7-6c1fad72fd78" },
	K_TeamColor{ TeamName = "Red", SkinName = "https://core.trackmania.nadeo.live/storageObjects/f94a342c-cbd2-4136-953c-b5da764b487e" }
]

#Struct K_TeamConfig {
	Text ClubTag;
	Text Name;
	Integer Hue;
	Text SkinName;
	Ident ItemId;
}

declare K_TeamConfig[Integer] G_TeamConfigs;
declare Text[Integer] G_TeamNameOverrides;
declare Integer[Integer] G_TeamHueOverrides;
declare Ident[Integer] G_SkinIds;
declare Boolean G_UseClubTags;

Integer Private_GetClosestLutIndex(Integer Hue) {
	declare Integer LutIdx = 0;
	declare Integer Dist = 400;
	for (I, 0, 12) {
		declare Integer H = I * 30;
		if (ML::Abs(Hue - H) < Dist){
			Dist = ML::Abs(Hue - H);
			LutIdx = I;
		}
	}
	return LutIdx;
}

Void SetTeam1NameOverride(Text Name) {
	G_TeamNameOverrides[1] = Name;
}

Void SetTeam2NameOverride(Text Name) {
	G_TeamNameOverrides[2] = Name;
}

Void SetTeam1HueOverride(Integer Hue) {
	if (Hue < 0) {
		G_TeamHueOverrides[1] = C_Hue_UseDefault;
	} else {
		G_TeamHueOverrides[1] = ML::Min(Hue, 360);
	}
}

Void SetTeam2HueOverride(Integer Hue) {
	if (Hue < 0) {
		G_TeamHueOverrides[2] = C_Hue_UseDefault;
	} else {
		G_TeamHueOverrides[2] = ML::Min(Hue, 360);
	}
}

Void UseClubTags(Boolean UseClubTags) {
	G_UseClubTags = UseClubTags;
}

Void LoadSkins() {
	foreach (Index => TeamColor in C_TeamColorLut) {
		G_SkinIds[Index] = ItemList_AddWithSkin("CarSport", TeamColor.SkinName);
	}
}

Ident GetSkinModelIdForTeam(Integer Clan) {
	if (G_TeamConfigs.existskey(Clan)) {
		return G_TeamConfigs[Clan].ItemId;
	}
	return NullId;
}

Text GetSkinNameForTeam(Integer Clan) {
	if (G_TeamConfigs.existskey(Clan)) {
		return G_TeamConfigs[Clan].SkinName;
	}
	return "";
}

Integer GetDefaultTeamHue(Integer Clan) {
	return C_DefaultTeamHues.get(Clan - 1, C_DefaultTeamHue);
}

Text GetDefaultTeamName(Integer Clan) {
	declare Text Teamname = "???";
	if (G_TeamConfigs.existskey(Clan)) {
		declare Integer Idx = Private_GetClosestLutIndex(G_TeamConfigs[Clan].Hue);
		Teamname = C_TeamColorLut[Idx].TeamName;
	}
	return "Team " ^ Teamname;
}

Void Private_OverrideForcedOrEmptyFields() {
	for(Clan, 1, 2) {
		if (G_TeamHueOverrides.get(Clan, C_Hue_UseDefault) != C_Hue_UseDefault) { // Forced
			G_TeamConfigs[Clan].Hue = G_TeamHueOverrides[Clan];
		} else if (G_TeamConfigs[Clan].Hue == C_Hue_UseDefault) { // Was empty
			G_TeamConfigs[Clan].Hue = GetDefaultTeamHue(Clan);
		}

		if (G_TeamNameOverrides.get(Clan, "") != "") { // Forced
			G_TeamConfigs[Clan].Name = G_TeamNameOverrides[Clan];
		} else if (G_TeamConfigs[Clan].Name == "") { // Was empty
			G_TeamConfigs[Clan].Name = GetDefaultTeamName(Clan);
		}
	}
}

Void Private_SetTeamNamesFromClubTags(){
	for (Clan, 1, 2) {
		declare Boolean ClubTagMissmatch;
		declare Text[] ClubTagsTeam;
		foreach (Player in Players) {
			if (Player.CurrentClan == Clan && !Player.RequestsSpectate) { // Is playing
				if (ClubTagsTeam.count == 0) {
					ClubTagsTeam.add(Player.User.ClubTag);
				} else if (!ClubTagsTeam.exists(Player.User.ClubTag)) {
					ClubTagMissmatch = True;
					break;
				}
			}
		}

		if (!ClubTagMissmatch && ClubTagsTeam.count == 1) {
			G_TeamConfigs[Clan].ClubTag = ClubTagsTeam[0];
			G_TeamConfigs[Clan].Name = TL::StripFormatting(ClubTagsTeam[0]);
		}
	}
}

Void Private_SetTeamHueFromMap() {
	declare metadata Real[Integer] FlagRush_Meta_TeamHues for Map;
	for (Clan, 1, 2) {
		declare Integer Hue = C_Hue_UseDefault;
		if (FlagRush_Meta_TeamHues.existskey(Clan)) {
			if (FlagRush_Meta_TeamHues[Clan] < 0) {
				Hue = C_Hue_UseDefault;
			} else {
				declare Real HueReal = ML::Min(FlagRush_Meta_TeamHues[Clan], 1.);
				Hue = ML::NearestInteger(HueReal * 360);
			}
		}
		G_TeamConfigs[Clan].Hue = Hue;
	}
}

Void Private_ApplyToTeams() {
	for (Clan, 1, 2) {
		Teams[Clan - 1].Name = G_TeamConfigs[Clan].Name;

		declare Integer Hue = G_TeamConfigs[Clan].Hue;

		declare Vec3 LightColor = NCL::GetLightColor(Hue * 1.);
		declare Vec3 MidColor = NCL::GetMidColor(Hue * 1.);
		declare Vec3 DarkColor = NCL::GetDarkColor(Hue * 1.);

		Teams[Clan - 1].ColorPrimary = MidColor;
		Teams[Clan - 1].ColorSecondary = MidColor;

		declare netwrite Vec3 Net_FlagRush_LightColor for Teams[Clan - 1];
		Net_FlagRush_LightColor = LightColor;
		declare netwrite Vec3 Net_FlagRush_MidColor for Teams[Clan - 1];
		Net_FlagRush_MidColor = MidColor;
		declare netwrite Vec3 Net_FlagRush_DarkColor for Teams[Clan - 1];
		Net_FlagRush_DarkColor = DarkColor;

		declare Integer LutIdx = Private_GetClosestLutIndex(Hue);
		G_TeamConfigs[Clan].SkinName = C_TeamColorLut[LutIdx].SkinName;
		G_TeamConfigs[Clan].ItemId = G_SkinIds.get(LutIdx, NullId);
	}
}

Void Init() {
	G_TeamConfigs = [1 => K_TeamConfig{ Hue = C_Hue_UseDefault }, 2 => K_TeamConfig{ Hue = C_Hue_UseDefault }];
	if (Map != Null) Private_SetTeamHueFromMap();
	if (G_UseClubTags) Private_SetTeamNamesFromClubTags();
	Private_OverrideForcedOrEmptyFields();
	Private_ApplyToTeams();
}