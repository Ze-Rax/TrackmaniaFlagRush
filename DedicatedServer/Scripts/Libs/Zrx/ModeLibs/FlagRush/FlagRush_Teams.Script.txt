// #RequireContext CSmMode

// #Include "ColorLib"	as CL

// #Include "Libs/Zrx/Common/ColorUtils.Script.txt"														as ColorUtils
// #Include "Libs/Zrx/ModeLibs/Common/ClubTagUtils.Script.txt"									as ClubTagUtils
#Include "/Libs/Zrx/ModeLibs/FlagRush/UI/FlagRush_ColorPalette.Script.txt"	as Colors
#Include "/Libs/Rx/ModeLibs/Common/OkColors.Script.txt"	as OKCL
#Include "MathLib" as ML

#Const	C_DefaultTeamName						"Unknown Team"
#Const	C_DefaultTeamNames					[0 => "Team Teardrop",1 => "Team Feather"]
#Const	C_DefaultTeamHue						148
#Const	C_DefaultTeamHues						[0 => 240,1 => 20]

#Const C_SkinNames [
	"Skins/Models/CarSport/Stadium_Clan_pig.zip",
	"Skins/Models/CarSport/Stadium_Clan_ladybug.zip",
	"Skins/Models/CarSport/Stadium_Clan_fox.zip",
	"Skins/Models/CarSport/Stadium_Clan_bee.zip",
	"Skins/Models/CarSport/Stadium_Clan_grasshopper.zip",
	"Skins/Models/CarSport/Stadium_Clan_crocodile.zip",
	"Skins/Models/CarSport/Stadium_Clan_peafowl.zip",
	"Skins/Models/CarSport/Stadium_Clan_rabbit.zip",
	"Skins/Models/CarSport/Stadium_Clan_polar_bear.zip",//find a better skin for this hue
	"Skins/Models/CarSport/Stadium_Clan_dolphin.zip",
	"Skins/Models/CarSport/Stadium_Clan_butterfly.zip",
	"Skins/Models/CarSport/Stadium_Clan_octopus.zip",
	"Skins/Models/CarSport/Stadium_Clan_pig.zip"//wrap around
]
#Const C_OkHueSkinLut [
	2.63,
	29.2,
	52.8,
	110.,
	136.,
	142.,
	151.,
	195.,
	256.,
	264.,
	294.,
	328.,
	362.63
]


// #Const	C_HueOverlapThreshold				0.166
#Struct K_TeamConfig {
	Text ClubTag;
	Text Name;
	Integer Hue;
	Real OkHue;
	Text SkinName;
	Ident ItemId;
}

declare K_TeamConfig[Integer] G_TeamConfigs;
declare Text[Integer] G_TeamNameOverrides;
declare Integer[Integer] G_TeamHueOverrides;
declare Ident[Integer] G_SkinIds;
declare Boolean G_UseClubTags;

Void SetTeam1NameOverride(Text Name) {
	G_TeamNameOverrides[0] = Name;
}

Void SetTeam2NameOverride(Text Name) {
	G_TeamNameOverrides[1] = Name;
}

Void SetTeam1HueOverride(Integer Hue) {
	G_TeamHueOverrides[0] = Hue;
}

Void SetTeam2HueOverride(Integer Hue) {
	G_TeamHueOverrides[1] = Hue;
}

Void UseClubTags(Boolean UseClubTags) {
	G_UseClubTags = UseClubTags;
}

Void LoadSkins() {
	for(i, 0, C_SkinNames.count-1){
		G_SkinIds[i] = ItemList_AddWithSkin("CarSport", C_SkinNames[i]);
		// log(G_SkinIds[i]);
	}
}

Ident GetSkinModelIdForTeam(Integer Team) {
	if (G_TeamConfigs.existskey(Team)) {
		// log("WTF?"^G_TeamConfigs[Team].ItemId);
		return G_TeamConfigs[Team].ItemId;
	}
	return NullId;
}

Text GetSkinNameForTeam(Integer Team) {
	if (G_TeamConfigs.existskey(Team)) {
		return G_TeamConfigs[Team].SkinName;
	}
	return "";
}

Integer GetDefaultTeamHue(Integer Clan) {
	return C_DefaultTeamHues.get(Clan-1, C_DefaultTeamHue);
}

Text GetDefaultTeamName(Integer Clan) {
	return C_DefaultTeamNames.get(Clan-1, C_DefaultTeamName);
}

Void Private_Configs_OverrideEmptyFields() {
	for(TeamIndex, 0, 1) {
		if (G_TeamNameOverrides.get(TeamIndex, "") != "")	// Forced
			G_TeamConfigs[TeamIndex].Name = G_TeamNameOverrides[TeamIndex];
		else if (G_TeamConfigs[TeamIndex].Name == "")					// Was empty
			G_TeamConfigs[TeamIndex].Name = GetDefaultTeamName(TeamIndex + 1);

		if(G_TeamHueOverrides.get(TeamIndex, -1) != -1){
			declare Integer Hue = G_TeamHueOverrides[TeamIndex];
			G_TeamConfigs[TeamIndex].Hue = Hue;
			G_TeamConfigs[TeamIndex].OkHue = OKCL::RgbToOkLch(OKCL::HsvToRgb(<Hue/360.,1.,1.>)).Z;
		}
		else if (G_TeamConfigs[TeamIndex].Hue == 0){
			declare Integer Hue = GetDefaultTeamHue(TeamIndex + 1);
			G_TeamConfigs[TeamIndex].Hue = Hue;
			G_TeamConfigs[TeamIndex].OkHue = OKCL::RgbToOkLch(OKCL::HsvToRgb(<Hue/360.,1.,1.>)).Z;
		}
	}
}

Void Private_Configs_ApplyToTeams() {

	for (TeamIndex, 0, 1) {
		Teams[TeamIndex].Name = G_TeamConfigs[TeamIndex].Name;
		declare Real OkHue = G_TeamConfigs[TeamIndex].OkHue;

		declare Vec3 LightColor =   OKCL::GetLightColor(OkHue);
		declare Vec3 MidColor = OKCL::GetMidColor(OkHue);
		declare Vec3 DarkColor = OKCL::GetDarkColor(OkHue);

		Teams[TeamIndex].ColorPrimary = MidColor;
		Teams[TeamIndex].ColorSecondary = MidColor;

		declare netwrite Vec3 Net_FlagRush_LightColor for Teams[TeamIndex];
		Net_FlagRush_LightColor = LightColor;
		declare netwrite Vec3 Net_FlagRush_MidColor for Teams[TeamIndex];
		Net_FlagRush_MidColor = MidColor;
		declare netwrite Vec3 Net_FlagRush_DarkColor for Teams[TeamIndex];
		Net_FlagRush_DarkColor = DarkColor;

		declare Integer id = 0;
		declare Real dist = 400.;
		for(i, 0, C_OkHueSkinLut.count-1){
			if(ML::Abs(OkHue-C_OkHueSkinLut[i]) < dist){
				dist = ML::Abs(OkHue-C_OkHueSkinLut[i]);
				id = i;
			}
		}
		G_TeamConfigs[TeamIndex].SkinName = C_SkinNames[id];
		G_TeamConfigs[TeamIndex].ItemId = G_SkinIds.get(id,NullId);
	}
}

Void Init() {
/* 	if (G_UseClubTags) {
		Private_Configs_InitFromClubTags();
	} else {
		G_TeamConfigs = [Teams[0] => K_TeamConfig{}, Teams[1] => K_TeamConfig{}];
	} */
	G_TeamConfigs = [0 => K_TeamConfig{}, 1 => K_TeamConfig{}];
	Private_Configs_OverrideEmptyFields();
	Private_Configs_ApplyToTeams();
}