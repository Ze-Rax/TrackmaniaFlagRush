#Include	"Libs/Zrx/ModeLibs/FlagRush/UI/FlagRush_ColorPalette.Script.txt" as Colors

#Const C_FlagMarkerHeightIndicatorThreshold	7.

/**
 *	Manialink script functions to get the actual (non-adjusted) color for a team or clan.
 */
Text GetTeamColorNetreadFunctions() {
	return """
			Vec3 GetTeamPrimaryColor(CTeam Team) {
				if (Team == Null) return {{{ Colors::C_NeutralLight }}};
				declare netread Vec3 Net_FlagRush_PrimaryColor for Team;
				return Net_FlagRush_PrimaryColor;
			}

			Vec3 GetTeamPrimaryColor(Integer Clan) {
				if (Clan <= 0 || Clan > Teams.count) return {{{ Colors::C_NeutralLight }}};
				return GetTeamPrimaryColor(Teams[Clan-1]);
			}
""";
}

/**
 * Manialink script functions to map a login to different objects.
 */
Text GetLoginMappingFunctions() {
	return """
		CSmPlayer GetPlayer(Text Login) {
			foreach (Player in Players) if (Player.User.Login == Login) return Player;
			return Null;
		}
	""";
}

/**
 * Manialink script functions to get the position and rotation values for a player.
 */
Text GetPlayerPositionFunctions() {
	return """
		Vec3 GetPlayerPosition(CSmPlayer Player) {
			if(Player == InputPlayer || !Player.UseCrudeExtrapolation) return Player.Position;
			declare netread Vec3 Net_Position for Player;
			return Net_Position;
		}

		Real GetPlayerYaw(CSmPlayer Player) {
			if (Player == InputPlayer || !Player.UseCrudeExtrapolation) return Player.AimYaw;
			declare netread Real Net_Yaw for Player;
			return Net_Yaw;
		}
	""";
}

/**
 * Returns the manialink framemodel with a transparent for the logo of FlagRush
 */
Text GetLogoTransparentFramemodel() {
	return """
	<framemodel id="flagrush-logo">
		<label size="20 5" text="$i$sFlag" valign="center" halign="right" textfont="GameFontExtraBold" pos="-4 2"/>
		<label size="10 10" text="$i$sï€¤" valign="center" halign="center" textfont="GameFontExtraBold" textsize="6"/>
		<label size="20 5" text="$i$sRush" valign="center" halign="left" textfont="GameFontExtraBold" pos="4 -2"/>
	</framemodel>
	""";
}