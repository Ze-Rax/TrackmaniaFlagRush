#Include "ColorLib" as CL

#Include "Libs/Zrx/ModeLibs/FlagRush/UI/FlagRush_ColorPalette.Script.txt" as Colors
#Include "Libs/Zrx/ModeLibs/FlagRush/UI/FlagRush_UIShared.Script.txt" as FlagRush_UIShared
#Include "Libs/Zrx/ModeLibs/FlagRush/FlagRush_Map.Script.txt" as FlagRush_Map

#Const C_CustomEventType_CarSelected "CarSelected"

#Const FlagRush_Map::C_CarId_Stadium as C_CarId_Stadium
#Const FlagRush_Map::C_CarId_Snow as C_CarId_Snow
#Const FlagRush_Map::C_CarId_Rally as C_CarId_Rally

#Struct K_UICarButtonParameter {
	Text Label;
	Text Icon;
}

// Keys should be same a C_CarId_*
#Const C_UICarButtonParameters [
	"stadium" => K_UICarButtonParameter{Label = "Stadium", Icon = ""},
	"snow" => K_UICarButtonParameter{Label = "Snow", Icon = "❄"},
	"rally" => K_UICarButtonParameter{Label = "Rally", Icon = ""}
]


Text GetManialink() {
	declare Text[] EnabledCars = FlagRush_Map::GetEnabledCarIds();

	// Reset player selection if previous seleciton is not valid anymore
	foreach (Player in Players) {
		declare netwrite Text Net_SelectedCarId for Player = "";
		if (!EnabledCars.exists(Net_SelectedCarId)) {
			Net_SelectedCarId = "";
		}
	}

	declare Text CarFrameInstances;
	for (Index => CarId in EnabledCars) {
		declare K_UICarButtonParameter Parameters = C_UICarButtonParameters.get(CarId, K_UICarButtonParameter{});
		CarFrameInstances ^= """<frameinstance modelid="car-option" id="car-option-{{{ CarId }}}" pos="{{{ 6 + Index * 13 }}} 0" data-name="{{{ Parameters.Label }}}" data-icon="{{{ Parameters.Icon }}}" data-id="{{{ CarId }}}" />""";
	}

	return """
<?xml version="1.0" encoding="utf-8" standalone="yes" ?>
<manialink version="3" name="FlagRush_CarSelection">
	<framemodel id="car-option">
		<quad id="background" size="12 12" halign="center" valign="center" opacity="{{{ Colors::C_TransparentBackgroundOpacity }}}" bgcolor="{{{ CL::RgbToHex3(Colors::C_NeutralDark) }}}"/>
		<label id="icon" size="8 8" pos="0 2" halign="center" valign="center" text="?" textfont="GameFontSemiBold" textsize="7" textcolor="{{{ CL::RgbToHex6(Colors::C_NeutralLight) }}}"/>
		<label id="name" size="13 4" pos="0 -4" halign="center" valign="center" text="<CAR NAME>" textfont="GameFontSemiBold" textsize="1" textcolor="{{{ CL::RgbToHex6(Colors::C_NeutralLight) }}}"/>
		<quad id="activator" size="15 15" halign="center" valign="center" opacity="0" scriptevents="1"/>
	</framemodel>

	<frame pos="-103 -47.5" id="car-selection">
		<frame id="car-options" pos="0 -3.5">
			{{{ CarFrameInstances }}}
		</frame>
	</frame>

	<script><!--
	#Include "MathLib" as ML

	#Struct K_CarOptionControls {
		CMlFrame Frame;
		CMlQuad Background;
		CMlQuad Activator;
		CMlLabel Name;
		CMlLabel Icon;
	}

	#Struct K_CarOption {
		K_CarOptionControls Controls;
		Text Name;
		Text Icon;
		Text Id;
	}

	declare CMlFrame G_CarSelectionFrame;
	declare K_CarOption[] G_CarOptions;

	{{{ FlagRush_UIShared::GetTeamColorNetreadFunctions() }}}

	Void AssignMlControlReferences() {
		G_CarSelectionFrame = (Page.GetFirstChild("car-selection") as CMlFrame);

		declare CarOptionsFrame = (Page.GetFirstChild("car-options") as CMlFrame);
		foreach (OptionFrameControl in CarOptionsFrame.Controls) {
			declare OptionFrame = (OptionFrameControl as CMlFrame);
			declare OptionControls = K_CarOptionControls{
				Frame = OptionFrame,
				Background = (OptionFrame.GetFirstChild("background") as CMlQuad),
				Activator = (OptionFrame.GetFirstChild("activator") as CMlQuad),
				Name = (OptionFrame.GetFirstChild("name") as CMlLabel),
				Icon = (OptionFrame.GetFirstChild("icon") as CMlLabel)
			};
			G_CarOptions.add(K_CarOption{
				Controls = OptionControls,
				Name = OptionFrame.DataAttributeGet("name"),
				Icon = OptionFrame.DataAttributeGet("icon"),
				Id = OptionFrame.DataAttributeGet("id")
			});
		}
	}

	Void InitMlControls() {
		foreach (CarOption in G_CarOptions) {
			CarOption.Controls.Name.Value = CarOption.Name;
			CarOption.Controls.Icon.Value = CarOption.Icon;
			declare Text CarId for CarOption.Controls.Activator = "";
			CarId = CarOption.Id;
		}
	}

	Integer GetSelectedCarIndex() {
		declare netread Text Net_SelectedCarId for UI = "";
		foreach (Index => Option in G_CarOptions) {
			if (Option.Id == Net_SelectedCarId) {
				return Index;
			}
		}
		return 0;
	}

	Void UpdateHighlight() {
		declare Integer SelectedCarIndex = GetSelectedCarIndex();
		foreach (Index => Option in G_CarOptions) {
			if (Index == SelectedCarIndex) {
				Option.Controls.Background.BgColor = GetTeamDarkColor(InputPlayer.RequestedClan);
			} else {
				Option.Controls.Background.BgColor = {{{ Colors::C_NeutralDark }}};
			}
		}
	}

	Boolean IsModelForced() {
		// TODO: This must be fixed to be more flexible
		declare netread Text FlagRush_Net_ForceModel for Teams[0] = "";
		declare CarIds = ["{{{ C_CarId_Stadium }}}", "{{{ C_CarId_Snow }}}", "{{{ C_CarId_Rally }}}"];
		return CarIds.exists(FlagRush_Net_ForceModel);
	}

	main() {
		AssignMlControlReferences();
		InitMlControls();
		while (True) {
			yield;
			if (!PageIsVisible) {
				continue;
			}

			if (IsSpectator || IsModelForced()) {
				G_CarSelectionFrame.Hide();
				continue;
			}

			G_CarSelectionFrame.Show();
			UpdateHighlight();

			foreach(Event in PendingEvents) {
				if (Event.Type == CMlScriptEvent::Type::MouseClick) {
					declare Text CarId for Event.Control = "";
					if (CarId != "") {
						SendCustomEvent("{{{ C_CustomEventType_CarSelected }}}", [CarId]);
					}
				}
			}

			foreach (Event in Input.PendingEvents) {
				if (Event.Type == CInputEvent::EType::PadButtonPress) {
					if (Event.Button == CInputEvent::EButton::RightStick_Right) {
						declare SelectedCarIndex = ML::Clamp(GetSelectedCarIndex() + 1, 0, G_CarOptions.count - 1);
						SendCustomEvent("{{{ C_CustomEventType_CarSelected }}}", [G_CarOptions[SelectedCarIndex].Id]);
					} else if (Event.Button == CInputEvent::EButton::RightStick_Left) {
						declare SelectedCarIndex = ML::Clamp(GetSelectedCarIndex() - 1, 0, G_CarOptions.count - 1);
						SendCustomEvent("{{{ C_CustomEventType_CarSelected }}}", [G_CarOptions[SelectedCarIndex].Id]);
					}
				}
			}
		}
	}
	--></script>
</manialink>
	""";
}