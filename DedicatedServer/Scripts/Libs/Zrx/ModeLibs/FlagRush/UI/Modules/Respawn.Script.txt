//#RequireContext CSmMlScriptIngame
#Include "Libs/Zrx/ModeLibs/FlagRush/UI/FlagRush_UIShared.Script.txt" as UIShared
#Include "Libs/Zrx/ModeLibs/FlagRush/FlagRush_Common.Script.txt" as FlagRush_Common

Text GetManialink() {
	return """
	<?xml version="1.0" encoding="utf-8" standalone="yes" ?>
	<manialink version="3" name="FlagRush_RespawnCounter">
		<frame pos="-40 -50" id="rs-frame" hidden="1">
			<label pos="40 -5" z-index="2" size="80 6" text="" halign="center" style="TextCardSmall" textsize="3" valign="center2" id="rs-label" textcolor="fff"/>
			<frame id="rs-gauge-frame" z-index="1" size="80 10">
				<quad pos="0 0" size="80 10" id="rs-gauge" style="UICommon64_1" substyle="BgFrame1" colorize="004" opacity="0.7"/>
			</frame>
		<quad pos="0 0" z-index="0" size="80 10" id="rs-gauge2" style="UICommon64_1" substyle="BgFrame1" colorize="000" opacity="0.7"/>
		</frame>

	<script><!--
	#Include "MathLib" as ML

	{{{ UIShared::GetTeamColorNetreadFunctions() }}}

	main() {
		declare CMlFrame RespawnFrame = (Page.GetFirstChild("rs-frame") as CMlFrame);
		declare CMlFrame GaugeFrame = (Page.GetFirstChild("rs-gauge-frame") as CMlFrame);
		declare CMlLabel RespawnLabel = (Page.GetFirstChild("rs-label") as CMlLabel);
		declare CMlQuad  RespawnQuad = (Page.GetFirstChild("rs-gauge") as CMlQuad);

		wait(InputPlayer != Null);

		declare netread Integer Net_SpawnDate for InputPlayer;
		declare Integer TotalRespawnTime = -1;
		while (True) {
			yield;

			declare Integer MillisRemaining = Net_SpawnDate - GameTime + {{{ FlagRush_Common::C_SpawnAnimDuration }}};
			if (Net_SpawnDate <= 0 || MillisRemaining <= 0 || IsSpectator) {
				RespawnFrame.Hide();
				TotalRespawnTime = -1;
				continue;
			}

			if (TotalRespawnTime <= 0) {
				TotalRespawnTime = MillisRemaining;
			}
			declare Real Ratio = ML::NearestReal(MillisRemaining) / TotalRespawnTime;

			// Display
			declare Integer SecondsRemaining = MillisRemaining / 1000 + 1;
			GaugeFrame.Size.X = ML::Clamp(80. * Ratio, 0., 80.);
			RespawnQuad.Colorize = GetTeamDarkColor(InputPlayer.CurrentClan);
			RespawnLabel.SetText("Respawning in " ^ SecondsRemaining ^ "...");
			RespawnFrame.Show();
		}
	}
	--></script>
	</manialink>
	""";
}